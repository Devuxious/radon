plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java'
}

group 'xyz.itzsomebody'
version '3.0.0-SNAPSHOT'

defaultTasks 'clean', 'testClasses', 'shadowJar'
sourceCompatibility = 11

def main = 'xyz.itzsomebody.radon.Main'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar() {
    archiveClassifier.set(null)

    // Relocate lib classes
    relocate 'org.objectweb.asm', 'xyz.itzsomebody.radon.lib.org.objectweb.asm'
    relocate 'org.yaml.snakeyaml', 'xyz.itzsomebody.radon.lib.org.yaml.snakeyaml'
    exclude 'module-info.class'

    manifest { attributes 'Main-Class': main }
}

repositories {
    mavenCentral()
    maven { url "http://oss.sonatype.org/content/groups/public/" } // SnakeYAML
}

dependencies {
    implementation 'org.yaml:snakeyaml:1.25-SNAPSHOT'

    for (lib in ['asm', 'asm-tree', 'asm-commons', 'asm-util']) {
        implementation group: 'org.ow2.asm', name: lib, version: '7.2'
    }

    testImplementation 'junit:junit:4.12'
}
